USE master
IF EXISTS(select * from sys.databases where name='db_Library')
DROP DATABASE db_Library

CREATE DATABASE db_Library;
GO

USE [db_Library]

IF OBJECT_ID('dbo.BOOK_LOANS', 'U') IS NOT NULL
	DROP TABLE BOOK_LOANS, BOOK_COPIES, BORROWER, LIBRARY_BRANCH, BOOK_AUTHORS, BOOKS, PUBLISHER;
GO

CREATE TABLE PUBLISHER
(
	PublisherName varchar(128) PRIMARY KEY NOT NULL
	,Address varchar(128) NOT NULL
	,Phone varchar(24) NOT NULL
);

CREATE TABLE BOOKS
(
	BookID INT PRIMARY KEY NOT NULL IDENTITY(1000,1)
	,Title varchar(250) NOT NULL
	,PublisherName varchar(128) NOT NULL CONSTRAINT fk_Publisher_Books FOREIGN KEY REFERENCES PUBLISHER(PublisherName) ON UPDATE CASCADE ON DELETE CASCADE
);

CREATE TABLE BOOK_AUTHORS
(
	BookID INT CONSTRAINT fk_Books_BookAuthors FOREIGN KEY REFERENCES BOOKS(BookID) ON UPDATE CASCADE ON DELETE CASCADE
	,AuthorName varchar(64) NOT NULL
);

CREATE TABLE LIBRARY_BRANCH
(
	BranchID INT PRIMARY KEY NOT NULL IDENTITY(5000,1)
	,BranchName varchar(128) NOT NULL
	,Address varchar(128) NOT NULL
);

CREATE TABLE BORROWER
(
	CardNo INT PRIMARY KEY NOT NULL IDENTITY(1,1)
	,Name varchar(128) NOT NULL
	,Address varchar(128) NOT NULL
	,Phone varchar(24) NOT NULL
);

CREATE TABLE BOOK_COPIES
(
	BookID INT NOT NULL CONSTRAINT fk_Books_BookCopies FOREIGN KEY REFERENCES BOOKS(BookID) ON UPDATE CASCADE ON DELETE CASCADE
	,BranchID INT NOT NULL CONSTRAINT fk_LibraryBranch_BookCopies FOREIGN KEY REFERENCES LIBRARY_BRANCH(BranchID) ON UPDATE CASCADE ON DELETE CASCADE
	,Number_Of_Copies INT NOT NULL
	,CONSTRAINT ck_UnsignedIntCopies CHECK (Number_Of_Copies >= 0)
	,CONSTRAINT uk_BooksBranch UNIQUE(BookID, BranchID)
);

CREATE TABLE BOOK_LOANS
(
	BookID INT NOT NULL CONSTRAINT fk_Books_BookLoans FOREIGN KEY REFERENCES BOOKS(BookID) ON UPDATE CASCADE ON DELETE CASCADE
	,BranchID INT NOT NULL CONSTRAINT fk_LibraryBranch_BookLoans FOREIGN KEY REFERENCES LIBRARY_BRANCH(BranchID) ON UPDATE CASCADE ON DELETE CASCADE
	,CardNo INT NOT NULL CONSTRAINT fk_Borrower_BookLoans FOREIGN KEY REFERENCES BORROWER(CardNo) ON UPDATE CASCADE ON DELETE CASCADE
	,DateOut DATE NOT NULL DEFAULT(GETDATE())
	,DateDue DATE NOT NULL DEFAULT(DATEADD(d,30,GETDATE()))
	,CONSTRAINT ck_Valid_DueDate CHECK(DateDue > DateOut)
);

PRINT 'db_Library has been built';
GO